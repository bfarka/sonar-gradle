#!groovy
// The above triggers groovy syntax highlighting in vim

plugins {
  id "com.gradle.plugin-publish" version "0.9.0"
  // Eat your own dog food :)
  id "org.sonarqube" version "1.0"
  id "com.github.hierynomus.license" version "0.11.0"
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'org.sonarqube.gradle'
version = '1.1-SNAPSHOT'
description = 'Gradle plugin to help analyzing projects with SonarQube'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  jcenter()
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile gradleApi()
  compile 'org.codehaus.sonar.runner:sonar-runner-api:2.4'
  compile 'com.google.guava:guava:17.0'
  testCompile localGroovy()
  testCompile 'junit:junit:4.11'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  compile 'com.android.tools.build:gradle:1.3.1'
  testCompile('org.spockframework:spock-core:1.0-groovy-2.3') {
    exclude group: 'org.codehaus.groovy'
  }
}

pluginBundle {
  website = 'http://docs.sonarqube.org/display/SONAR/Analyzing+with+Gradle'
  vcsUrl = 'https://github.com/SonarCommunity/sonar-gradle'

  description = 'Gradle plugin for running SonarQube analysis.'
  tags = ['sonarqube', 'sonar', 'quality', 'qa']

  plugins {

    sonarqubePlugin {
      id = 'org.sonarqube'
      displayName = 'Gradle SonarQube plugin'
    }
  }

  mavenCoordinates {
    groupId = "org.sonarqube.gradle"
    artifactId = "gradle-sonarqube-plugin"
  }
}

sonarqube {
  properties {
    property 'sonar.projectName', 'Gradle SonarQube Plugin'
  }
}

license {
  strictCheck true
}
